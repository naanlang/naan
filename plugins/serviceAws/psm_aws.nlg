/*
 * psm_aws.nlg
 * serviceAws
 *
 *     PSM connector for AWS storage for browser and NodeJS.
 *
 * column positioning:                          //                          //                      !
 *
 * Copyright (c) 2020-2021 by Richard C. Zulch
 *
 */


/*
 * psmaConnector
 *
 *     Make a PSM connector for AWS bucket-based tables.
 *
 */

closure psmaConnector(psm, local connClassID, connector, watch) {
    connClassID = "S3DB"
    require("./dbt_aws.nlg")
    connector = new(object, this)
    connector.psm = psm
    connector.vault = psm.vault(connClassID, connector)
    connector.label = "AWS S3"
    watch = psm.watchable(connector)

    // uifields
    //
    // Report the UI fields we need to add a resource
    connector.uifields = function uifields() {
        [{
            label:          "Key ID"
            placeholder:    "IAM keyID"
            key:            "keyID" },
        {
            label:          "Key Secret"
            placeholder:    "IAM keySecret"
            key:            "keySecret"
            type:           "password" },
        {
            label:          "AWS region"
            placeholder:    "e.g. us-east-1"
            key:            "region" },
        {
            label:          "Bucket"
            placeholder:    "e.g. myBucketName"
            key:            "bucketName" },
        {
            label:          "Endpoint"
            placeholder:    "custom URL"
            key:            "endpoint" }
        ]
    }

    // findResource
    //
    // Find a resource with the specified contents, returning the resID or false.
    connector.findResource = function findResource(resource, local resID, error, creds) {
        for resID in listResources().1 {
            `(error, creds) = connector.vault.accessResource(resID)
            if creds && (!resource.region || resource.region == creds.region)
                && (!resource.bucketName || resource.bucketName == creds.bucketName)
                && (!resource.endpoint || resource.endpoint == creds.endpoint)
                && (!resource.label || resource.label == creds.label)
                return (resID)
        }
        false
    }

    // writeResource
    //
    // Add or update credentials for a named resource. If no resID is specified this adds a new
    // resource; otherwise it updates an existing one. The resource is a dictionary comprising these
    // keys:
    //      label       - [optional] label we use for this resource
    //      keyID       - AWS IAM keyID
    //      keySecret   - AWS IAM keySecret
    //      region      - AWS region
    //      bucketName  - AWS bucketName
    //      endpoint    - custom URL
    //      hidden      - [optional] don't enumerate to user
    //      locked      - [optional] don't allow user delete
    // The return value is an (error, resID) tuple. Error, if non-false, is a dictionary of field
    // keys and error diagnostic strings.
    connector.writeResource = function writeResource(resource, resID,
        local creds, errors, error, data, change) {
        // badField - test for a bad string
        function badField(str) { !string(str) || str.trim().length == 0 }

        creds = { 
            keyID: resource.keyID,
            keySecret: resource.keySecret,
            region: resource.region,
            bucketName: resource.bucketName
        }
        if resource.endpoint
            creds.endpoint = resource.endpoint
        errors = { }
        if resource.label
            creds.label = resource.label
        else
            creds.label = resource.region.concat("-", resource.bucketName)
        if badField(creds.label)
            errors.label = "invalid label"
        else if !resID && findResource({ label: creds.label })
            errors.label = "duplicate name"
        if badField(resource.keyID)
            errors.keyID = "required field"
        if badField(resource.keySecret)
            errors.keySecret = "required field"
        if badField(resource.region)
            errors.region = "required field"
        if badField(resource.bucketName)
            errors.bucketName = "required field"
        if resource.endpoint && badField(resource.endpoint)
            errors.endpoint = "invalid field"
        if length(errors) > 0
            error = errors
        else {
            if resource.hidden
                creds.hidden = resource.hidden
            if resource.locked
                creds.locked = resource.locked
            if resID {
                `(error, data) = connector.vault.updateResource(resID, creds)
                change = { changed: [resID] }
            } else {
                resID = UUID()
                `(error, data) = connector.vault.addResource(resID, creds)
                change = { added: [resID] }
            }
            if data {
                watch.notify(connClassID, change)
                data = resID
            }
        }
        list(error, data)
    }

    // addResource
    //
    // Add a new resource, returning a standard error tuple per writeResource.
    connector.addResource = function addResource(resource) {
        writeResource(resource)
    }

    // updateResource
    //
    // Update an existing resource, returning a standard error tuple per writeResource.
    connector.updateResource = function updateResource(resID, resource) {
        writeResource(resource, resID)
    }
    
    // getResource
    //
    // Get credentials for a named resource, matching the dictionary semantics of addResource. The
    // result is a standard result tuple.
    connector.getResource = function getResource(resID, local error, creds, resource) {
        `(error, creds) = connector.vault.accessResource(resID)
        if error
            return (list(error))
        resource = {
            classID: connClassID
            label: creds.label
        }
        if string(creds.keyID)
            resource.keyID = creds.keyID
        if string(creds.keySecret)
            resource.keySecret = creds.keySecret
        if string(creds.region)
            resource.region = creds.region
        if string(creds.bucketName)
            resource.bucketName = creds.bucketName
        if string(creds.endpoint)
            resource.endpoint = creds.endpoint
        list(false, resource)
    }

    // deleteResource
    //
    // Delete credentials for a resource
    connector.deleteResource = function deleteResource(resID) {
        connector.vault.deleteResource(resID)
        watch.notify(connClassID, { deleted: [resID] })
        list(false, { ok: true})
    }
        
    // listResources
    //
    // List saved resources in our database
    connector.listResources = function listResources() {
        connector.vault.listResources()
    }
    
    // info
    //
    // Return info for a resource.
    connector.info = function info(resID, local error, creds, info) {
        info = {
            classID: "AWS S3"
            type: "AWS S3 bucket"
            services: ["DBT", "FS"]
        }
        `(error, creds) = connector.vault.accessResource(resID)
        if creds {
            if creds.label
                info.name = creds.label
            if creds.locked
                info.locked = true
            if creds.hidden
                info.hidden = true
            info.where = creds.bucketName.concat(" (", creds.region, ")")
        }
        list(false, info)
    }

    // access
    //
    // Return an S3 access object, which is the credentials to access an AWS S3 bucket.

    connector.access = function access(resID, local error, creds, params, s3b) {
        `(error, creds) = connector.vault.accessResource(resID)
        if error
            return (list(error))                                            // can't access that resource
        if !awsSDK.S3
            return (list(Error("S3 connector: no AWS SDK available")))
        params = {
            region:                     creds.region
            credentials: {
                accessKeyId:            creds.keyID
                secretAccessKey:        creds.keySecret
            }
        }
        s3b = {
            bucketName:                 creds.bucketName
            region:                     creds.region
        }
        if creds.endpoint {
            params.endpoint = creds.endpoint
            s3b.endpoint = creds.endpoint
        }
        s3b.s3 = xnew(awsSDK.S3, params)
        list(false, s3b)
    }

    // connect
    //
    // Connect to an AWS bucket filesystem, returning a view.
    
    connector.connect = function connect(resID, service, args,
        local error,  s3b, db, rootpath, table) {
        `(error, s3b) = connector.access(resID)
        if !error
            `(error, db) = S3DB(s3b, args.1)                                // get S3 bucket as a database
        if error
            list(error)
        else if service == "NideDB"
            list(false, db)
        else if service == "NideFS" {
            rootpath = args.0                                               // connect to a NideFS filesystem
            if !rootpath
                return (Error("mandatory rootpath missing"))
            `(error, table) = db.table(rootpath, args.1)                    // args.1 is table options; see dawsTable
            if error
                list(error)
            else
                require("frameworks/storage/psm_dbtables.nlg").FSview(table, rootpath)
        } else
            list(Error("unsupported service", service))
    }

    // finis

    psm.register(connClassID, connector)
    connector
};


/*
 * psmaInit
 *
 *     Initialize the AWS module.
 *
 */

function psmaInit(local manifest) {

    manifest = `(psmaConnector, psmaInit)

    Naan.module.build(module.id, "psm_aws", function(modobj, compobj) {
        compobj.manifest = manifest
        modobj.exports.AwsConnector = psmaConnector
        require("./serviceAws.nlg")
    })
} ();
