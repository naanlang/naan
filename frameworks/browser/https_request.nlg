/*
 * https_request.nlg
 *
 *     Https operations for the browser.
 *
 * column positioning:                          //                          //                      !
 *
 * Copyright (c) 2021-2024 by Richard C. Zulch
 *
 */


/*
 * HttpsApiRequest
 *
 * Perform an API request from the browser. This operates the same as HttpsApiRequest for NodeJS.
 *
 * Options:
 *  {
 *      method:         <string>                // HTTP method (defaults to GET/PUT)
 *      query:          <dictionary>            // query variables added to the URL
 *      putdata:        <data>                  // data to put
 *      contentType:    <string>                // sets Content-Type header, otherwise auto-determined
 *      encoding:       <string>                // "binary" to receive binary data as arrayBuffer
 *      range:          <string>                // sets range header
 *      headers:        [`(key, value)]         // add header(s) to the request, overriding defaults
 *      keepalive:      <boolean>               // true to send data at unload
 *      retrier:        <function>              // retry selected failures
 *      debug:          <boolean>               // log errors and status results
 *  }
 *
 * The return is a result tuple comprising:
 *      (<error>,                               -- false on success
 *      <content>,                              -- returned data
 *      {
 *          status:     <integer>               -- http response status
 *          headers:    <array>                 -- headers if available
 *      })
 *
 */

closure HttpsApiRequest(url, options,
    local delayf, urlq, fetchOptions, putdata, item, error, response, 
        contentType, content, headers) {
    global(window, common)
    //
    // defRetrier -- backoff and retry selected errors
    //
    function defRetrier(error, headers, local retryAfter, retryms, waitms) {
        if member(error.status, `(408, 500, 502, 503, 504)) {               // retriable errors
            retryAfter = headers["retry-after"]
            if retryAfter {
                retryms = toint(retryAfter) * 1000
                if !retryms
                    retryms = Date(retryAfter).getTime() - Date.now()
                if retryms < 0
                    retryms = false
            }
            if !delayf
                delayf = common.ExbackFactory(12,30)                        // 12s max backoff, 30s deadline
            waitms = delayf()
            if !waitms || waitms < retryms
                waitms = retryms
            if options.debug
                debuglog("HttpsApiRequest.defRetrier: wait ${waitms} msec using retry-after ${retryAfter}")
            if waitms                                                       // return false to give up
                sleep(waitms)                                               // return true from sleep to retry
        }
    }

    options = merge({ retrier: defRetrier }, options)
    if options.query
        urlq = url.concat(EncodeQuery("?", options.query))
    else
        urlq = url
    putdata = options.putdata
    fetchOptions = {
        mode: 'cors'
        cache: 'no-cache'
        credentials: 'same-origin'
        headers: { }
        redirect: 'follow'
        referrerPolicy: 'no-referrer'
        keepalive: options.keepalive || undefined
    }
    if options.method
        fetchOptions.method = options.method
    else if putdata
        fetchOptions.method = "POST"
    else
        fetchOptions.method = "GET"
    if putdata {
        if options.contentType
            fetchOptions.headers['Content-Type'] = options.contentType
        else if jsTypedArray(options.putdata)
            fetchOptions.headers['Content-Type'] = "application/octet-stream"
        else if member(typeof(options.putdata), `(dictionary, array, xobject)) {
            fetchOptions.headers['Content-Type'] = "application/json"
            `(error, putdata) = JsonStringify(options.putdata)
            if error
                return (list(Error("HttpsApiRequest encode:", url)))
        }
        fetchOptions.body = putdata
    }
    if options.range
        fetchOptions.headers.Range = options.range
    if array(options.headers)
        for item in options.headers
            fetchOptions.headers[item.0] = item.1
    //
    // perform the fetch and return when complete
    //
    loop {                                                                  // loop for retries
        `(error, response) = await(window.fetch(urlq, fetchOptions))
        if error {
            if options.debug
                debuglog("HttpsApiRequest error:", url, "error:", error)
            return (list(Error("HttpsApiRequest fetch failed:", error, url)))
        }
        headers = { }
        response.headers.forEach(function(value, key) {
            headers[key.toLowerCase()] = value
        })
        if response.status < 200 || response.status >= 299 {                // 299 is "cancelled"
            if options.debug
                debuglog("HttpsApiRequest status:", url, response.statusCode)
            error = Error("HttpsApiRequest status:", url, response.status, {
                status: response.status
            })
            if options.retrier(error, headers)
                continue                                                    // optional retry/delay logic
            else
                return (list(error, false, {
                    headers: headers
                    status: response.status
                }))
        } else
            break
    }
    contentType = response.headers.get("content-type")
    if contentType.startsWith("application/json")
        || contentType.startsWith("application/octet-stream") && options.encoding == "json" {
        `(error, content) = await(response.text())
        if !error
            `(error, content) = JsonParse(content)
            if error
                error = Error("HttpsApiRequest decode:", url, error)
            else
                content = new(content)
            if array(content) {                                             // deencapsulate to get API result
                content = totuple(content)
                error = content.0
                content = content.1
            }
    }
    else if options.encoding == "binary"
        `(error, content) = await(response.arrayBuffer())
    else
        `(error, content) = await(response.text())
    list(error, content, {
        headers: headers
        status: response.status
        contentType: contentType
    })
};


/*
 * https_browserInit
 *
 * Initialize HTTPS request operations for browsers.
 *
 */

function https_browserInit(local manifest) {
    manifest = `(HttpsApiRequest, https_browserInit)

    Naan.module.build(module.id, "https_request", function(modobj, compobj) {
        require("browser.nlg")
        common = require("../common/utils.nlg")
        compobj.manifest = manifest
        modobj.exports.HttpsApiRequest = HttpsApiRequest
    })
}();
