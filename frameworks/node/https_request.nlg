/*
 * https_node.nlg
 *
 *     Https operations for NodeJS.
 *
 * column positioning:                          //                          //                      !
 *
 * Copyright (c) 2021-2023 by Richard C. Zulch
 *
 */


/*
 * HttpsApiRequest
 *
 * Perform an API request from in NodeJS. This operates the same as HttpsApiRequest for browsers.
 *
 * Options:
 *  {
 *      method:         <string>                // HTTP method (defaults to GET/PUT)
 *      query:          <dictionary>            // query variables added to the URL
 *      putdata:        <data>                  // data to put
 *      contentType:    <string>                // sets Content-Type header, otherwise auto-determined
 *      range:          <string>                // sets range header
 *      headers:        [`(key, value)]         // add header(s) to the request, overriding defaults
 *      auth:           <string>                // username:password string
 *      cacert:         <data>                  // ca for verifying peer
 *      debug:          <boolean>               // log errors and status results
 *  }
 *
 */

closure HttpsApiRequest(url, options,
    local urlq, reqOptions, putdata, item, xurl, pending, chunks, request) {
    if options.query
        urlq = url.concat(EncodeQuery("?", options.query))
    else
        urlq = url
    putdata = options.putdata
    reqOptions = { }
    if options.method
        reqOptions.method = options.method
    else if putdata
        reqOptions.method = "POST"
    else
        reqOptions.method = "GET"
    reqOptions.headers = { }
    if putdata {
        if options.contentType
            reqOptions.headers['Content-Type'] = options.contentType
        else if jsTypedArray(options.putdata)
            reqOptions.headers['Content-Type'] = "application/octet-stream"
        else if member(typeof(options.putdata), `(dictionary, array, xobject)) {
            reqOptions.headers['Content-Type'] = "application/json"
            `(error, putdata) = JsonStringify(options.putdata)
            if error
                return (list(Error("HttpsApiRequest encode:", url)))
        }
        reqOptions.body = putdata
    }
    if array(options.headers)
        for item in options.headers
            reqOptions.headers[item.0] = item.1
    if options.auth
        reqOptions.auth = options.auth
    if options.cacert
        reqOptions.ca = options.cacert
    if options.allowSelfSigned {
        xurl = nodeUrl.parse(url)
        reqOptions.agent = xnew(nodeHttps.Agent({
            host: xurl.hostname
            port: xurl.port
            path: xurl.path
            rejectUnauthorized: false
            ca: [ selfSignedRootCaPemCrtBuffer ]
        }))
    }
    pending = new(nonce)
    chunks = []
    request = nodeHttps.request(url, reqOptions, closure (response) {
        response.on("data", function (chunk) {
            chunks.push(chunk)
        })
        response.on("end", function (local content, error, headers) {
            content = js.g.Buffer.concat(chunks)
            if response.statusCode < 200 || response.statusCode >= 300 {
                if options.debug
                    debuglog("HttpsApiRequest status:", url, response.statusCode, "message:", content)
                pending.signal(list(Error("HttpsApiRequest statusCode:", url, response.statusCode, {
                    status: response.statusCode
                })))
                return
            }
            if response.complete {
                if response.headers["content-type"].startsWith("application/json") {
                    `(error, content) = JsonParse(content.toString())
                    if error
                        error = Error("HttpsApiRequest decode:", url, error)
                    else
                        content = new(content)
                    if array(content) {                                     // deencapsulate to get API result
                        content = totuple(content)
                        error = content.0
                        content = content.1
                    }
                }
            }
            else
                error = Error("HttpsApiRequest terminated prematurely:", url, response.statusCode)
            pending.signal(list(error, content, {
                headers: response.headers
                status: response.statusCode
            }))
        })
    })
    request.on("error", function hre(error) {
        if options.debug
            debuglog("HttpsApiRequest error:", url, "error:", error)
        error = Error("HttpsApiRequest failed:", url, error)
        pending.signal(list(error))
    })
    if putdata
        request.write(putdata)
    request.end()
    pending.wait()
};


/*
 * https_nodeInit
 *
 * Initialize HTTPS request operations for NodeJS.
 *
 */

function https_nodeInit(local manifest) {
    manifest = `(HttpsApiRequest, https_nodeInit)

    Naan.module.build(module.id, "https_request", function(modobj, compobj) {
        require("./node.nlg")
        compobj.manifest = manifest
        modobj.exports.HttpsApiRequest = HttpsApiRequest
    })
}();
