/*
 * running.nlg
 * Naanlib/frameworks/running
 *
 *     Root compoment for running execution instances.
 *
 * column positioning:                          //                          //                      !
 *
 * Copyright (c) 2021-2023 by Richard C. Zulch
 *
 */


/*
 * rootproc
 *
 *     Return the root procdef of the specified procdef.
 *
 */

function rootproc(procdef, local parent) {
    loop {                                                                  // can only find out proc
        parent = procdef.1@\.parent
        if !parent
            break
        procdef = parent.proc
    }
    procdef
};


/*
 * baseproc
 *
 *     Return the base procdef if a closure or macro, or the procedure symbol itself otherwise.
 * The base is the original procedure from which a closure or macro is instantiated.
 *
 */

function baseproc(procdef) {
    if procdef.1@\.base.proc
        { }
    else
        procdef
};


/*
 * pathmatch
 *
 *     Given a parent symbol and a tuple of child procedures within it, return the child procedure
 * named by the path in the tuple. Each element in the child procedures is either a symbol == to the
 * procedure or, in the case of lambdas, a tuple comprising the procedure symbol and an integer
 * specifying the nth matching child. This is necessary because a procedure can have any number of
 * children named lambda, and we need to pick the right ones even though we don't have identical
 * symbols.
 *
 */

function pathmatch(parent, path, local segment, nth, child) {
    if empty(path)
        return (parent)
    segment = pop(path)
    if tuple(segment) {                                                     // select nth instance of matching symbol
        nth = segment.1
        segment = segment.0
    } else
        nth = 0
    for child in parent@\.children
        if child == segment && nth-- == 0
            return (pathmatch(child, path))
    false
};


/*
 * procpath
 *
 *     Return a tuple of parent prcoedure symbols for the specified child procedure. Note that this
 * path includes the parent but pathmatch above requires the parent as a separate argument. As noted
 * above, the segments of the path are either a procedure symbol, or a tuple of symbol and integer.
 *
 */

function procpath(procdef, local path, segment, parent, peers, nth, peer) {
    loop {
        segment = procdef.1
        parent = segment@\.parent
        if segment == lambda && parent {                                    // lambdas all have same name
            peers = parent@\.children
            nth = 0
            for peer in peers {
                if peer === segment
                    break
                if peer == segment
                    ++nth
            }
            push(list(segment, nth), path)                                  // we are the nth of the matching ones
        } else
            push(segment, path)
        if !parent
            break
        procdef = parent.proc
    }
    path
};


/*
 * procname
 *
 *     Return the full name of possibly-nested procedure.
 *
 */

function procname(name, local parent) {
    parent = name@\.parent
    name = tostring(name)
    if parent
        procname(parent).concat(".", name)
    else
        name
};


/*
 * procstring
 *
 *     Return a string representing a procedure and its parameters.
 * Options:
 *  {
 *      fullname: true                                                      // list name path for nested procedures
 *      namespace: true                                                     // prepend name with namespace
 *  }
 *
 */
    
function procstring(procdef, options, local name, output, parm) {
    if options.fullname || !defined(options, `fullname)
        name = procname(procdef.1)                                          // default is full name
    else
        name = procdef.1
    if options.namespace || !defined(options, `namespace)                   // default includes namespace
        name = procdef.1.namespace.tostring.concat("::", name)
    if !procdef.2 || !car(procdef.2)
        return (tostring(procdef.0).concat(" ", name, "()"))
    if atom(procdef.2)
        return (tostring(procdef.0).concat(" ", name, " ", procdef.2))
    output = tostring(procdef.0).concat(" ", name, "(")
    for parm in procdef.2 {
        if !parm
            break
        output = output.concat(parm, ", ") }
    output = output.slice(0,-2)
    output.concat(")")
}


/*
 * runnInit
 *
 *     Initialize the module.
 *
 */

function runnInit(local manifest) {
    manifest = `(rootproc, baseproc, pathmatch, procpath, procname, procstring, runnInit)

    Naan.module.build(module.id, "running", closure(modobj, compobj) {
        require("../common").LiveImport()
        compobj.manifest = manifest
    })
} ();
